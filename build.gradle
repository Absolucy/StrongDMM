plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'org.jmailen.kotlinter' version '1.26.0'
}

application {
    group 'io.github.spair'
    version '0.0.10'
    mainClassName = 'strongdmm.StrongDMM'
}

repositories {
    jcenter()
    mavenCentral()
}

ext {
    lwjglVersion = '3.2.3'
    trove4jVersion = '3.0.3'
    gsonVersion = '2.8.6'
    pngjVersion = '2.1.0'
    imguiVersion = '1.75-0.7.1'
    logabackVersion = '1.2.3'
    slf4jVersion = '1.7.30'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation "net.sf.trove4j:trove4j:$trove4jVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "ar.com.hjg:pngj:$pngjVersion"

    implementation "ch.qos.logback:logback-core:$logabackVersion"
    implementation "ch.qos.logback:logback-classic:$logabackVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    implementation "io.imgui.java:binding:$imguiVersion"
    implementation "io.imgui.java:lwjgl3:$imguiVersion"
    runtimeOnly "io.imgui.java:natives-linux:$imguiVersion"
    runtimeOnly "io.imgui.java:natives-windows:$imguiVersion"

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    ['', '-opengl', '-glfw', '-stb', '-nfd'].each {
        implementation "org.lwjgl:lwjgl$it:$lwjglVersion"
        runtimeOnly "org.lwjgl:lwjgl$it::natives-linux"
        runtimeOnly "org.lwjgl:lwjgl$it::natives-windows"
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}

runShadow {
    jvmArgs(['-Dsdmmparser.path=./libs'])
}
