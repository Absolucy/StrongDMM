plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'org.jmailen.kotlinter' version '1.26.0'
    id 'com.gradle.build-scan' version '2.0.2'
}

application {
    group 'io.github.spair'
    version '0.0.5'
    mainClassName = 'io.github.spair.strongdmm.MainKt'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.github.spair:dmm-io:1.5'
    implementation 'ar.com.hjg:pngj:2.1.0'
    implementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
    implementation 'org.scijava:swing-checkbox-tree:1.0.2'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.7.0'
    shadow 'org.lwjgl.lwjgl:lwjgl:2.9.3'
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlwaysIf(System.getenv('CI') != null)
}

wrapper {
    gradleVersion = '5.4.1'
}

processResources {
    filesMatching('*.html') {
        // Replace '${version}' placeholder with actual version
        filter { line -> line.replace('${version}', version) }
    }
}
