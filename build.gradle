import org.gradle.internal.os.OperatingSystem

plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
    id 'org.jmailen.kotlinter' version '1.26.0'
}

application {
    group 'io.github.spair'
    version '0.0.10'
    mainClassName = 'strongdmm.StrongDMM'
}

repositories {
    jcenter()
    mavenCentral()
}

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.nativesType = "natives-linux"
        break
    case OperatingSystem.WINDOWS:
        project.ext.nativesType = "natives-windows"
        break
}

ext {
    lwjglVersion = '3.2.3'
    dmmioVersion = '1.6.1'
    trove4jVersion = '3.0.3'
    minimalJsonjVersion = '0.9.5'
    pngjVersion = '2.1.0'
    imguiVersion = '1.74-0.4'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "io.github.spair:dmm-io:$dmmioVersion"
    implementation "net.sf.trove4j:trove4j:$trove4jVersion"
    implementation "com.eclipsesource.minimal-json:minimal-json:$minimalJsonjVersion"
    implementation "ar.com.hjg:pngj:$pngjVersion"

    implementation "io.imgui.java:binding:$imguiVersion"
    implementation "io.imgui.java:lwjgl3:$imguiVersion"
    implementation "io.imgui.java:$nativesType:$imguiVersion"

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    ['', '-opengl', '-glfw', '-stb', '-nfd'].each {
        implementation "org.lwjgl:lwjgl$it:$lwjglVersion"
        runtimeOnly "org.lwjgl:lwjgl$it::$nativesType"
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}

runShadow {
    jvmArgs(['-Dsdmmparser.path=./libs'])
}
